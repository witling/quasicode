start: (_NEWLINE | statement)*

%ignore /[\t \f]+/                  // ignore whitespace
%ignore /\\[\t \f]*\r?\n/         // ignore whatever
%declare _INDENT _DEDENT
_NEWLINE: ( /\r?\n[\t ]*/ )+

STRING: /".*?(?<!\\)"/
IDENT: /[a-zA-Z_][\w\?!]*/
NUMBER: /\d+/

//add: "+"
//sub: "-"
//mul: "*"
//div: "/"
//mod: "modulo"
//cmp: "das" "ist"
//lt: "<"
//_calc_op: add | sub | mul | div | mod
//_compare_op: cmp | lt

// ?sign_comp: ("+" | "-")? atom
?sign_comp: atom

add: calc_comp "+" sign_comp
sub: calc_comp "-" sign_comp
mul: calc_comp "*" sign_comp
div: calc_comp "/" sign_comp
mod: calc_comp "modulo" sign_comp
?calc_comp: sign_comp
         | add
         | sub
         | mul
         | div
         | mod

cmp: compare_comp "das" "ist" calc_comp 
lt: compare_comp "<" calc_comp
?compare_comp: calc_comp
            | cmp
            | lt

!objty: "menge" | "liste"
construct_args: value+
construct: objty ("mit" construct_args)?

value: NUMBER | STRING | IDENT | construct

index: value "bei" value
slice_from: value "von" value
slice_till: (value | slice_from) "bis" value
slice: slice_from | slice_till

call: IDENT expression+
wexpression: IDENT | index | slice
?atom: value 
          | index 
          | slice 
          | "(" expression ")" 
          | call
expression: compare_comp

// language constructs

lhassign: wexpression "ist" expression
rhassign: expression "also" wexpression
assign: lhassign | rhassign
break: "patrick!"
import: "use" IDENT
return: expression "und" "fertig"
nop: "oettinger"

// block statements

block: simple_statement | _NEWLINE _INDENT statement+ _DEDENT
marker_main: "action" "please"
declare_args: IDENT+
declare: "und" "zwar" IDENT ("mit" declare_args)? marker_main? block
loop: "das" "holen" "wir" "nach" block
else_branch: "ach" "kris." block
elif_branch: "kris??" expression block
if_branch: "kris?" expression block elif_branch* else_branch?

?simple_statement: (import | break | return | assign | expression | nop) _NEWLINE
?block_statement: (declare | if_branch | loop)
?statement: simple_statement | block_statement
